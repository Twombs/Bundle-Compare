#cs ----------------------------------------------------------------------------

 AutoIt Version: 3.3.14.2
 Author:         Timboli (aka TheSaint)

 Script Function:  Extract game titles from Itch.io bundle JSON file listings
	Template AutoIt script.

#ce ----------------------------------------------------------------------------

#include <Constants.au3>
#include <GUIConstantsEx.au3>
#include <WindowsConstants.au3>
#include <ButtonConstants.au3>
#include <ComboConstants.au3>
#include <Misc.au3>
#include <File.au3>
#include <String.au3>
#include <Array.au3>

_Singleton("bundle-compare-thsaint")

Global $ans, $attrib, $bundle, $c, $cnt, $created, $dir, $drv, $entries, $entry, $fext, $fname, $games, $inifile, $itchfile
Global $itchlist, $num, $palestine, $path, $racial, $read, $sect, $sects, $split, $titfile, $title, $titles, $ukraine, $version

$inifile = @ScriptDir & "\Settings.ini"
$itchfile = @ScriptDir & "\Itch Bundles.tsv"
$itchlist = @ScriptDir & "\Itch Bundles.ini"
$titfile = @ScriptDir & "\Titles.tsv"

$created = "March 2022"
$version = "v1.1"

$racial = IniRead($inifile, "Racial Justice", "path", "")
If $racial = "" Then
	$racial = "C:\Users\Timbo\Downloads\2022\03\08\Itch.io\520_games.json"
	IniWrite($inifile, "Racial Justice", "path", $racial)
EndIf
$palestine = IniRead($inifile, "Palestinian Aid", "path", "")
If $palestine = "" Then
	$palestine = "C:\Users\Timbo\Downloads\2022\03\08\Itch.io\902_games.json"
	IniWrite($inifile, "Palestinian Aid", "path", $palestine)
EndIf
$ukraine = IniRead($inifile, "Ukraine Assist", "path", "")
If $ukraine = "" Then
	$ukraine = "C:\Users\Timbo\Downloads\2022\03\08\Itch.io\1316_games.json"
	IniWrite($inifile, "Ukraine Assist", "path", $ukraine)
EndIf

$ans = MsgBox(262177 + 256, "Process Query", "OK = Extract." & @LF & "CANCEL = View Options.", 0)
If $ans = 1 Then
	SplashTextOn("", "Extracting!", 100, 80, Default, Default, 33)
	If FileExists($itchlist) Then FileMove($itchlist, $itchlist & ".bak", 1)
	If FileExists($itchfile) Then FileMove($itchfile, $itchfile & ".bak", 1)
	If FileExists($titfile) Then FileMove($titfile, $titfile & ".bak", 1)
	$games = 0
	While 1
		$path = $racial
		If FileExists($path) Then
			AddToThelist()
		Else
			MsgBox(262192, "Path Error", "The required '520_games.json' file not found.", 0)
			ExitLoop
		EndIf
		$path = $palestine
		If FileExists($path) Then
			AddToThelist()
		Else
			MsgBox(262192, "Path Error", "The required '902_games.json' file not found.", 0)
			ExitLoop
		EndIf
		$path = $ukraine
		If FileExists($path) Then
			AddToThelist()
		Else
			MsgBox(262192, "Path Error", "The required '1316_games.json' file not found.", 0)
			ExitLoop
		EndIf
		ExitLoop
	WEnd
	SplashOff()
	MsgBox(262192, "Game Titles", "Found = " & $games, 0)
Else
	If FileExists($itchfile) Then
		ViewerOptions()
	ElseIf FileExists($itchlist) Then
		SplashTextOn("", "Converting!", 100, 80, Default, Default, 33)
		$entries = ""
		$read = FileRead($itchlist)
		If $read <> "" Then
			$split = StringSplit($read, '[', 1)
			$cnt = $split[0]
			MsgBox(262192, "Games Count", $cnt - 1 & @LF & @LF & "(auto close)", 3)
			For $c = 2 To $cnt
				$sect = $split[$c]
				If $sect <> "" Then
					$entry = StringSplit($sect, ']', 1)
					If $entry[0] > 1 Then
						$entry = $entry[1]
						If $entry <> "" Then
							$title = IniRead($itchlist, $entry, "title", "")
							If $title <> "" Then
								$racial = IniRead($itchlist, $entry, "1", "na")
								$palestine = IniRead($itchlist, $entry, "2", "na")
								$ukraine = IniRead($itchlist, $entry, "3", "na")
								$entry = $title & @TAB & $racial & @TAB & $palestine & @TAB & $ukraine
								If $entries = "" Then
									$entries = $entry
								Else
									$entries = $entries & @CRLF & $entry
								EndIf
							EndIf
						EndIf
					EndIf
				EndIf
			Next
		EndIf
		FileWrite($itchfile, $entries)
		;
		SplashTextOn("", "Loading!", 100, 80, Default, Default, 33)
		If FileExists($itchfile) Then
			_FileReadToArray($itchfile, $entries, 1, @TAB)
			$ans = MsgBox(262177, "Sort Choice", "OK = Title." & @LF & "CANCEL = No Sort.", 0)
			If $ans = 1 Then
				_ArraySort($entries, 0, 1, 0, 0)
			EndIf
			SplashOff()
			_ArrayDisplay($entries, "Itch Bundles", "", 0, @TAB, "Titles|Racial Justice|Palestinian Aid|Ukraine Assist", 350, $COLOR_SKYBLUE)
		Else
			SplashOff()
			MsgBox(262192, "Convert Error", "No resulting 'Itch Bundles.tsv' file found.", 0)
		EndIf
	Else
		MsgBox(262192, "File Error", "No files (using Extract) have been created yet.", 0)
	EndIf
EndIf

Exit

Func ViewerOptions()
	Local $Button_info, $Button_view, $Checkbox_save, $Checkbox_view, $Combo_limit, $Combo_sort, $Group_limit, $Group_sort
	Local $choice, $choices, $GUI_main, $items, $other, $save, $savfile, $tmpfile, $view, $viewfile, $views
	;
	; Script generated by GUIBuilder Prototype 0.9
	Const $style = BitOR($WS_OVERLAPPED, $WS_CAPTION, $WS_SYSMENU, $WS_VISIBLE, $WS_CLIPSIBLINGS, $WS_MINIMIZEBOX)
	$GUI_main = GuiCreate("View Options - Bundle Compare", 410, 100, -1, -1, $style, $WS_EX_TOPMOST)
	; CONTROLS
	$Group_sort = GuiCtrlCreateGroup("Sorting", 10, 10, 120, 55)
	$Combo_sort = GuiCtrlCreateCombo("", 20, 30, 100, 21)
	;
	$Group_limit = GuiCtrlCreateGroup("Limit", 140, 10, 120, 55)
	$Combo_limit = GuiCtrlCreateCombo("", 150, 30, 100, 21)
	;
	$Button_info = GuiCtrlCreateButton("Info", 270, 15, 50, 50)
	GUICtrlSetFont($Button_info, 9, 600)
	GUICtrlSetTip($Button_info, "Program Information!")
	;
	$Button_view = GuiCtrlCreateButton("VIEW", 330, 15, 70, 50)
	GUICtrlSetFont($Button_view, 9, 600)
	GUICtrlSetTip($Button_view, "View using the Sort and Limit options!")
	;
	$Checkbox_save = GUICtrlCreateCheckbox("Save any sorted or limited results to file", 15, 72, 200, 20)
	GUICtrlSetTip($Checkbox_save, "Save any sorted or limited results to file!")
	;
	$Checkbox_view = GUICtrlCreateCheckbox("View with another TAB program", 230, 72, 170, 20)
	GUICtrlSetTip($Checkbox_view, "View results with another (default) TAB program!")
	;
	; SETTINGS
	$choices = "none|Titles|Racial Justice|Palestinian Aid|Ukraine Assist"
	GUICtrlSetData($Combo_sort, $choices, "Titles")
	;
	$views = "ALL|Racial Justice|Palestinian Aid|Ukraine Assist"
	GUICtrlSetData($Combo_limit, $views, "all")
	;
	$save = IniRead($inifile, "Sorted Or Limited", "save", "")
	If $save = "" Then
		$save = 4
		IniWrite($inifile, "Sorted Or Limited", "save", $save)
	EndIf
	GUICtrlSetState($Checkbox_save, $save)
	;
	$other = IniRead($inifile, "Another Program", "view", "")
	If $other = "" Then
		$other = 4
		IniWrite($inifile, "Another Program", "view", $other)
	EndIf
	GUICtrlSetState($Checkbox_view, $other)

	GuiSetState(@SW_SHOWNORMAL)
	Do
		Switch GuiGetMsg()
			Case $GUI_EVENT_CLOSE
				GUIDelete($GUI_main)
				ExitLoop
			Case $Button_view
				; View using the Sort and Limit options
				GUISetState(@SW_MINIMIZE, $GUI_main)
				SplashTextOn("", "Loading!", 100, 80, Default, Default, 33)
				If Not FileExists($titfile) Then
					FileCopy($itchfile, $titfile, 1)
				EndIf
				$choice = GUICtrlRead($Combo_sort)
				$view = GUICtrlRead($Combo_limit)
				If $choice <> "none" Or $view <> "ALL" Then
					If $view = "ALL" Then
						$viewfile = $itchfile
						$savfile = $titfile
						$savfile = StringReplace($savfile, "Titles.tsv", "Titles_saved.tsv")
					ElseIf $view = "Racial Justice" Then
						$racial = @ScriptDir & "\Racial Justice.tsv"
						$viewfile = $racial
						FileDelete($viewfile)
						$items = ""
						_FileReadToArray($itchfile, $entries, 1)
						For $c = 1 To $entries[0]
							$entry = $entries[$c]
							If StringInStr($entry, "Racial Justice") > 0 And (StringInStr($entry, "Palestinian Aid") < 1 And StringInStr($entry, "Ukraine Assist") < 1) Then
								If $items = "" Then
									$items = $entry
								Else
									$items = $items & @CRLF & $entry
								EndIf
							EndIf
						Next
						FileWrite($viewfile, $items)
						$savfile = $racial
						$savfile = StringReplace($savfile, "Racial Justice.tsv", "Racial Justice_saved.tsv")
					ElseIf $view = "Palestinian Aid" Then
						$palestine = @ScriptDir & "\Palestinian Aid.tsv"
						$viewfile = $palestine
						FileDelete($viewfile)
						$items = ""
						_FileReadToArray($itchfile, $entries, 1)
						For $c = 1 To $entries[0]
							$entry = $entries[$c]
							If StringInStr($entry, "Palestinian Aid") > 0 And (StringInStr($entry, "Racial Justice") < 1 And StringInStr($entry, "Ukraine Assist") < 1) Then
								If $items = "" Then
									$items = $entry
								Else
									$items = $items & @CRLF & $entry
								EndIf
							EndIf
						Next
						FileWrite($viewfile, $items)
						$savfile = $palestine
						$savfile = StringReplace($savfile, "Palestinian Aid.tsv", "Palestinian Aid_saved.tsv")
					ElseIf $view = "Ukraine Assist" Then
						$ukraine = @ScriptDir & "\Ukraine Assist.tsv"
						$viewfile = $ukraine
						FileDelete($viewfile)
						$items = ""
						_FileReadToArray($itchfile, $entries, 1)
						For $c = 1 To $entries[0]
							$entry = $entries[$c]
							If StringInStr($entry, "Ukraine Assist") > 0 And (StringInStr($entry, "Palestinian Aid") < 1 And StringInStr($entry, "Racial Justice") < 1) Then
								If $items = "" Then
									$items = $entry
								Else
									$items = $items & @CRLF & $entry
								EndIf
							EndIf
						Next
						FileWrite($viewfile, $items)
						$savfile = $ukraine
						$savfile = StringReplace($savfile, "Ukraine Assist.tsv", "Ukraine Assist_saved.tsv")
					EndIf
					_FileReadToArray($viewfile, $entries, 1, @TAB)
					SplashTextOn("", "Sorting!", 100, 80, Default, Default, 33)
					If $choice = "Titles" Then
						_ArraySort($entries, 0, 1, 0, 0)
					ElseIf $choice = "Racial Justice" Then
						_ArraySort($entries, 0, 1, 0, 1)
					ElseIf $choice = "Palestinian Aid" Then
						_ArraySort($entries, 0, 1, 0, 2)
					ElseIf $choice = "Ukraine Assist" Then
						_ArraySort($entries, 0, 1, 0, 3)
					EndIf
					If $save = 1 Then
						_FileWriteFromArray($savfile, $entries, 1, Default, @TAB)
					EndIf
				Else
					_FileReadToArray($itchfile, $entries, 1, @TAB)
					$savfile = $titfile
				EndIf
				SplashOff()
				If $other = 4 Then
					_ArrayDisplay($entries, "Itch Bundles", "", 0, @TAB, "Titles|Racial Justice|Palestinian Aid|Ukraine Assist", 350, $COLOR_SKYBLUE)
					GUISetState(@SW_RESTORE, $GUI_main)
				ElseIf $other = 1 Then
					If $save = 4 And ($choice <> "none" Or $view <> "ALL") Then
						; Create a temp save file
						$tmpfile = @ScriptDir & "\Temp_saved.tsv"
						_FileWriteFromArray($tmpfile, $entries, 1, Default, @TAB)
						$savfile = $tmpfile
					EndIf
					ShellExecute($savfile)
				EndIf
			Case $Button_info
				; Program Information
				MsgBox(262208, "Program Information - Bundle Compare", _
					"This program Extracts game titles from Itch.io bundle JSON file listings." & @LF & @LF & _
					"Initially an INI file is created (Itch Bundles.ini), and from that a TAB" & @LF & _
					"separated file is then created (Itch Bundles.tsv), and then other TAB" & @LF & _
					"separated files (Titles.tsv etc) are created for Sort and View options." & @LF & @LF & _
					"To open a TAB delimited file with another program, you may need to" & @LF & _
					"associate the 'tsv' extension with it (i.e. Microsoft Works Spreadsheet)." & @LF & @LF & _
					"Â© " & $created & " created by Timboli. " & $version & " update.", 0, $GUI_main)
			Case $Checkbox_view
				; View results with another (default) TAB program
				If GUICtrlRead($Checkbox_view) = $GUI_CHECKED Then
					$other = 1
				Else
					$other = 4
				EndIf
				IniWrite($inifile, "Another Program", "view", $other)
			Case $Checkbox_save
				; Save any sorted or limited results to file
				If GUICtrlRead($Checkbox_save) = $GUI_CHECKED Then
					$save = 1
				Else
					$save = 4
				EndIf
				IniWrite($inifile, "Sorted Or Limited", "save", $save)
			Case Else
				;
		EndSwitch
	Until False
EndFunc ;=> ViewerOptions

Func AddToThelist()
	$attrib = FileGetAttrib($path)
	If StringInStr($attrib, "D") < 1 Then
		_PathSplit($path, $drv, $dir, $fname, $fext)
		If $fext = ".json" Then
			$read = FileRead($path)
			If $read <> "" Then
				$split = StringSplit($read, '"title":"', 1)
				$cnt = $split[0]
				MsgBox(262192, "Games Count", $cnt - 1 & @LF & @LF & "(auto close)", 3)
				$games = IniRead($itchlist, "Games", "total", "")
				If $games = "" Then
					$games = 0
					IniWrite($itchlist, "Games", "total", $games)
				EndIf
				$titles = ""
				For $c = 2 To $cnt
					$sect = $split[$c]
					$title = StringSplit($sect, '","', 1)
					If $title[0] > 1 Then
						$title = $title[1]
						$title = StringReplace($title, "&amp;", "&")
						$title = StringReplace($title, "[", "(")
						$title = StringReplace($title, "]", ")")
						$title = StringReplace($title, "\/", "/")
						;Dominique Pamplemousse
						$title = StringReplace($title, '\"', "'")
						;$title = StringReplace($title, '??', '')
						;While StringLeft($title, 1) = "?"
						;	$title = StringTrimLeft($title, 1)
						;	$title = StringStripWS($title, 1)
						;WEnd
						$title = StringSplit($title, '"', 1)
						$title = $title[1]
						$title = StringStripWS($title, 3)
						If $title <> "" Then
							IniWrite($itchlist, $title, "title", $title)
							$games = $games + 1
							;IniWrite($itchlist, $title, "count", $games)
							IniWrite($itchlist, "Games", "total", $games)
							If $fname = "520_games" Then
								$bundle = "Racial Justice"
								$num = 1
							ElseIf $fname = "902_games" Then
								$bundle = "Palestinian Aid"
								$num = 2
							ElseIf $fname = "1316_games" Then
								$bundle = "Ukraine Assist"
								$num = 3
							EndIf
							IniWrite($itchlist, $title, $num, $bundle)
						EndIf
						;?? Potato Thriller (Classic)	Racial Justice	na	na
						;If $titles = "" Then
						;	$titles = $title
						;Else
						;	$titles = $titles & " | " & $title
						;EndIf
					EndIf
				Next
				;MsgBox(262192, "Game Titles", "Found = " & $games & @LF & @LF & $titles, 0)
			EndIf
		Else
			MsgBox(262192, "Format Error", "File does not appear to be a json file.", 0)
		EndIf
	Else
		MsgBox(262192, "Usage Error", "Needs to be a json file not a folder.", 0)
	EndIf
EndFunc ;=> AddToThelist
